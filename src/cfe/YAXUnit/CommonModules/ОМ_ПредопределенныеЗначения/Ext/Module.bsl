#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТестовыйНабор("Ключи хеша предопределенного элемента")
			.ДобавитьТест("Тест_КлючиХеша_ФормированиеКлючейЭлемент")
			.ДобавитьТест("Тест_КлючиХеша_ФормированиеКлючейГруппа")
			.ДобавитьТест("Тест_КлючиХеша_ПустойМассив")
	;
	
КонецПроцедуры

Процедура Тест_КлючиХеша_ФормированиеКлючейЭлемент() Экспорт
	
	Поля = Новый Массив;
	Поля.Добавить("Поле1");
	Поля.Добавить("Поле2");
	Поля.Добавить("Служебный_Поле3");
	Поля.Добавить("Поле4");
	
	ИсключаемыеПоля = Новый Структура;
	ИсключаемыеПоля.Вставить("Поле2", Истина);
	
	Мокито.Обучение(пбп_ПредопределенныеЗначенияПереопределяемый)
		.Когда("ИсключаемыеПоляДляРасчетаХешаЭлемент")
		.Вернуть(ИсключаемыеПоля)
		.Прогон();
	
	Ожидаемое = Новый Структура("Поле1,Поле4");
	
	Результат = пбп_ПредопределенныеЗначения.КлючиХешаПредопределенногоЭлемента(Поля, Ложь);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Ожидаемое);
	
КонецПроцедуры

Процедура Тест_КлючиХеша_ФормированиеКлючейГруппа() Экспорт
	
	Поля = Новый Массив;
	Поля.Добавить("Поле1");
	Поля.Добавить("Поле2");
	
	ИсключаемыеПоля = Новый Структура;
	ИсключаемыеПоля.Вставить("Поле1", Истина);
	
	Мокито.Обучение(пбп_ПредопределенныеЗначенияПереопределяемый)
		.Когда("ИсключаемыеПоляДляРасчетаХешаГруппа")
		.Вернуть(ИсключаемыеПоля)
		.Прогон()
	;
	
	Ожидаемое = Новый Структура("Поле2");
	
	Результат = пбп_ПредопределенныеЗначения.КлючиХешаПредопределенногоЭлемента(Поля, Истина);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Ожидаемое);
	
КонецПроцедуры

Процедура Тест_КлючиХеша_ПустойМассив() Экспорт
	
	Поля = Новый Массив;
	
	ИсключаемыеПоля = Новый Структура;
	
	Мокито.Обучение(пбп_ПредопределенныеЗначенияПереопределяемый)
		.Когда("ИсключаемыеПоляДляРасчетаХешаЭлемент")
		.Вернуть(ИсключаемыеПоля)
		.Прогон();
	
	Ожидаемое = Новый Структура;
	
	Результат = пбп_ПредопределенныеЗначения.КлючиХешаПредопределенногоЭлемента(Поля);
	
	ЮТест.ОжидаетЧто(Результат).Равно(Ожидаемое);
	
КонецПроцедуры

#КонецОбласти