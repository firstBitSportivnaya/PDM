#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РегламентноеЗадание				= ПолучитьРегламентноеЗадание();
	РасписаниеРегламентногоЗадания	= РегламентноеЗадание.Расписание;
	ИспользоватьРегламентноеЗадание	= РегламентноеЗадание.Использование;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НастроитьДоступностьСсылкиНастройкиРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьРегламентноеЗаданиеПриИзменении(Элемент)
	
	НастроитьДоступностьСсылкиНастройкиРасписания();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеРегламентногоЗадания(Команда)
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеРасписанияРегламентногоЗаданияЗавершение", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область ВспомогательныеПроцедурыИФункции

&НаСервере
Функция ПолучитьРегламентноеЗадание()
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Ключ"		, "ОчисткаИсторииИнтеграции");
	ПараметрыЗадания.Вставить("Метаданные"	, Метаданные.РегламентныеЗадания.ОчисткаИсторииИнтеграции);
	МассивРегламентныхЗаданий = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыЗадания);
	
	Если МассивРегламентныхЗаданий.Количество() Тогда
		РегламентноеЗадание = МассивРегламентныхЗаданий[0];
	Иначе
		ПараметрыЗадания.Вставить("Использование"	, Истина);
		ПараметрыЗадания.Вставить("Расписание"		, Новый РасписаниеРегламентногоЗадания());
		
		РегламентноеЗадание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	КонецЕсли;
	
	Возврат РегламентноеЗадание;
	
КонецФункции

&НаКлиенте
Процедура НастроитьДоступностьСсылкиНастройкиРасписания()
	
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Доступность = ИспользоватьРегламентноеЗадание;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗаданияЗавершение(Расписание, ДополнительныеПараметры) Экспорт
	
	Если Расписание <> Неопределено Тогда
		РасписаниеРегламентногоЗадания = Расписание;
		ЗаписатьРегламентноеЗаданиеПослеИзмененияРасписания();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРегламентноеЗаданиеПослеИзмененияРасписания()
	
	РегламентноеЗадание					= ПолучитьРегламентноеЗадание();
	РегламентноеЗадание.Расписание		= РасписаниеРегламентногоЗадания;
	РегламентноеЗадание.Использование	= ИспользоватьРегламентноеЗадание;
	РегламентноеЗадание.Записать();
	
КонецПроцедуры

#КонецОбласти // ВспомогательныеПроцедурыИФункции
