// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПодготовитьФормуНаСервере();

КонецПроцедуры // ПриСозданииНаСервере

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	Если Не ЭтотОбъект.Параметры.Отбор.Свойство("Владелец") Тогда
		НастройкиДинамическогоСписка = Список.КомпоновщикНастроек.Настройки;
		ЭлементыОтбора = НастройкиДинамическогоСписка.Отбор.Элементы;
		
		ОтборПоВладельцу = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоВладельцу.ЛевоеЗначение	= Новый ПолеКомпоновкиДанных("Владелец");
		ОтборПоВладельцу.ВидСравнения	= ВидСравненияКомпоновкиДанных.ВСписке;
		ОтборПоВладельцу.Использование	= Истина;
		ОтборПоВладельцу.ПравоеЗначение	= ПолучитьСсылкиНаБотов();
	КонецЕсли;

КонецПроцедуры // ПодготовитьФормуНаСервере

&НаСервере
Функция ПолучитьСсылкиНаБотов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Боты.Ссылка
	|ИЗ
	|	Справочник.Боты КАК Боты";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции