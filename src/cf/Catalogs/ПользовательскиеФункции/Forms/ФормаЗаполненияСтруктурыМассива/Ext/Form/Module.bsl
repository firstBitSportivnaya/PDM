
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТаблицаСтруктуры.Очистить();
	
	Если Параметры.Свойство("ЗначенияСтруктуры") Тогда
		ЭтотОбъект.Заголовок = ЭтотОбъект.Заголовок + " структуры";
		фЗаполнениеМассива = Ложь;
		
		ЗначенияСтруктуры = ПолучитьИзВременногоХранилища(Параметры.ЗначенияСтруктуры);
		
		Для Каждого КлючИЗначение Из ЗначенияСтруктуры Цикл
			
			НоваяСтрока = ТаблицаСтруктуры.Добавить();
			НоваяСтрока.Параметр = КлючИЗначение.Ключ;
			
			МассивТиповЗначения = Новый Массив;
			МассивТиповЗначения.Добавить(ТипЗнч(КлючИЗначение.Значение));
			
			ОписаниеТипаЗначения = Новый ОписаниеТипов(МассивТиповЗначения);
			
			Если ОписаниеТипаЗначения = Новый ОписаниеТипов("Строка")
				И СтрНачинаетсяС(КлючИЗначение.Значение, "e1cib/tempstorage/") > 0 Тогда
				
				ДанныеХранилища = ПолучитьИзВременногоХранилища(КлючИЗначение.Значение);
				Если ТипЗнч(ДанныеХранилища) = Тип("Структура") Тогда
					НоваяСтрока.Значение = "Структура";
					НоваяСтрока.ЗначениеАдрес = КлючИЗначение.Значение;
				ИначеЕсли ТипЗнч(ДанныеХранилища) = Тип("Массив") Тогда
					НоваяСтрока.Значение = "Массив";
					НоваяСтрока.ЗначениеАдрес = КлючИЗначение.Значение;
				ИначеЕсли ТипЗнч(ДанныеХранилища) = Тип("ТаблицаЗначений") Тогда
					НоваяСтрока.Значение = "Таблица значений";
					НоваяСтрока.ЗначениеАдрес = КлючИЗначение.Значение;
				КонецЕсли;
				
			Иначе
				НоваяСтрока.Значение = КлючИЗначение.Значение;
				
			КонецЕсли;
		КонецЦикла;
	Иначе
		ЭтотОбъект.Заголовок = ЭтотОбъект.Заголовок + " массива";
		Элементы.ТаблицаСтруктурыПараметр.Видимость = Ложь;
		фЗаполнениеМассива = Истина;
		
		ЗначенияМассива = ПолучитьИзВременногоХранилища(Параметры.ЗначенияМассива);
		
		Для Каждого ЭлементМассива Из ЗначенияМассива Цикл
			
			НоваяСтрока = ТаблицаСтруктуры.Добавить();
			НоваяСтрока.Параметр = "";
			
			МассивТиповЗначения = Новый Массив;
			МассивТиповЗначения.Добавить(ТипЗнч(ЭлементМассива));
			
			ОписаниеТипаЗначения = Новый ОписаниеТипов(МассивТиповЗначения);
			
			Если ОписаниеТипаЗначения = Новый ОписаниеТипов("Строка")
				И СтрНачинаетсяС(ЭлементМассива, "e1cib/tempstorage/") > 0 Тогда
				
				ДанныеХранилища = ПолучитьИзВременногоХранилища(ЭлементМассива);
				Если ТипЗнч(ДанныеХранилища) = Тип("Структура") Тогда
					НоваяСтрока.Значение = "Структура";
					НоваяСтрока.ЗначениеАдрес = ЭлементМассива;
				ИначеЕсли ТипЗнч(ДанныеХранилища) = Тип("Массив") Тогда
					НоваяСтрока.Значение = "Массив";
					НоваяСтрока.ЗначениеАдрес = ЭлементМассива;
				ИначеЕсли ТипЗнч(ДанныеХранилища) = Тип("ТаблицаЗначений") Тогда
					НоваяСтрока.Значение = "Таблица значений";
					НоваяСтрока.ЗначениеАдрес = ЭлементМассива;
				КонецЕсли;
				
			Иначе
				НоваяСтрока.Значение = ЭлементМассива;
				
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьКэшЗначений(фКэшЗначений);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСтруктуры

&НаКлиенте
Процедура ТаблицаСтруктурыЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСтруктуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	СписокВыбора = фКэшЗначений.ДоступныеТипыЗначенияПараметра;
	РаботаСДиалогамиУПОКлиент.НачалоВыбораСоставного(
		ЭтотОбъект, Элемент, ТекущиеДанные, "Значение", СписокВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСтруктурыЗначениеОчистка(Элемент, СтандартнаяОбработка)
	
	Элементы.ТаблицаСтруктурыЗначение.ВыбиратьТип = Истина;
	
	ТекущиеДанные = Элементы.ТаблицаСтруктуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ЗначениеАдрес = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСтруктурыЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаСтруктуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСДиалогамиУПОКлиент.ОткрытиеСоставного(ЭтотОбъект, Элемент, СтандартнаяОбработка, ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВернутьПараметрыИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьКэшЗначений(КэшированныеЗначения)
	
	КэшированныеЗначения = Новый Структура;
	
	МассивТипов = ТаблицаСтруктуры.Выгрузить().Колонки.Значение.ТипЗначения.Типы();
	СписокТипов = ОбщегоНазначенияУПОСервер.ПодготовитьСписокВыбораТипа(МассивТипов);
	СписокТипов.Добавить(Тип("Структура"), "Структура");
	СписокТипов.Добавить(Тип("Массив"), "Массив");
	СписокТипов.Добавить(Тип("Соответствие"), "Таблица значений");
	СписокТипов.СортироватьПоПредставлению();
	
	КэшированныеЗначения.Вставить("ДоступныеТипыЗначенияПараметра", СписокТипов);
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьПараметрыИЗакрыть(ПараметрыФормы = Ложь)
	
	СоответствиеПараметров = Неопределено;
	
	Если Не ПараметрыФормы Тогда
		
		Если Не фЗаполнениеМассива Тогда
			СоответствиеПараметров = Новый Структура;
			Для Каждого Параметр Из ТаблицаСтруктуры Цикл
				Если ЗначениеЗаполнено(Параметр.ЗначениеАдрес) Тогда
					СоответствиеПараметров.Вставить(Параметр.Параметр, Параметр.ЗначениеАдрес);
				Иначе
					СоответствиеПараметров.Вставить(Параметр.Параметр, Параметр.Значение);
				КонецЕсли;
			КонецЦикла;
		Иначе
			СоответствиеПараметров = Новый Массив;
			Для Каждого Параметр Из ТаблицаСтруктуры Цикл
				Если ЗначениеЗаполнено(Параметр.ЗначениеАдрес) Тогда
					СоответствиеПараметров.Добавить(Параметр.ЗначениеАдрес);
				Иначе
					СоответствиеПараметров.Добавить(Параметр.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	АдресСоответствия = ПоместитьВоВременноеХранилище(СоответствиеПараметров, Новый УникальныйИдентификатор);
	
	Закрыть(АдресСоответствия);
	
КонецПроцедуры

#КонецОбласти