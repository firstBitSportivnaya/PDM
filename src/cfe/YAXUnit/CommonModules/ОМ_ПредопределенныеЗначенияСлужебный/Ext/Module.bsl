#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты
		.ДобавитьТестовыйНабор("Доп. параметры обработки предопределенных элементов")
			.ДобавитьТест("Тест_ДопПараметрыОбработкиКорректныеДанные")
			.ДобавитьТест("Тест_ДопПараметрыОбработкиПустаяТаблица")
			.ДобавитьТест("Тест_ДопПараметрыОбработкиМенеджерНеИзПодсистемыПредопределенных") // не относится к подсистеме предопределенных
				.СПараметрами("Справочник.пбп_НастройкиОтбораОбъектов")
				.СПараметрами("Справочник.пбп_ПользовательскиеФункции")
			.ДобавитьТест("Тест_ДопПараметрыОбработкиМенеджерРавенНеопределено")
		.ДобавитьТестовыйНабор("Менеджер входит в подсистему предопределенных")
			.ДобавитьТест("Тест_МенеджерВходитВПодсистему")
				.СПараметрами("Справочник.пбп_ИнтеграционныеПотоки", Истина)
				.СПараметрами("Справочник.пбп_НастройкиОтбораОбъектов", Ложь) // не относится к подсистеме предопределенных
				.СПараметрами("Справочник.пбп_ПользовательскиеФункции", Ложь) // не относится к подсистеме предопределенных
				.СПараметрами(Неопределено, Ложь)
	;
	
КонецПроцедуры

Процедура Тест_ДопПараметрыОбработкиКорректныеДанные() Экспорт
	
	МенеджерОбъекта = пбп_Переадресация
		.МенеджерОбъектаПоПолномуИмени("ПланВидовХарактеристик.пбп_ПредопределенныеЗначения");
	
	Таблица = пбп_ПредопределенныеЗначенияСлужебный.ТаблицаПредопределенныхЭлементов(МенеджерОбъекта);
	
	ПроверкаРезультатовОбработкиПредопределенных(МенеджерОбъекта, Таблица);
	
КонецПроцедуры

Процедура Тест_ДопПараметрыОбработкиПустаяТаблица() Экспорт
	
	МенеджерОбъекта = пбп_Переадресация
		.МенеджерОбъектаПоПолномуИмени("ПланВидовХарактеристик.пбп_ПредопределенныеЗначения");
	
	Таблица = Новый ТаблицаЗначений;
	
	ПроверкаРезультатовОбработкиПредопределенных(МенеджерОбъекта, Таблица);
	
КонецПроцедуры

Процедура Тест_ДопПараметрыОбработкиМенеджерНеИзПодсистемыПредопределенных(ИмяОбъекта) Экспорт
	
	МенеджерОбъекта = пбп_Переадресация.МенеджерОбъектаПоПолномуИмени(ИмяОбъекта);
	
	Таблица = Новый ТаблицаЗначений;
	
	ПроверкаРезультатовОбработкиПредопределенных(МенеджерОбъекта, Таблица, Ложь);
	
КонецПроцедуры

Процедура Тест_ДопПараметрыОбработкиМенеджерРавенНеопределено() Экспорт
	
	МенеджерОбъекта = Неопределено;
	
	Таблица = Новый ТаблицаЗначений;
	
	ПроверкаРезультатовОбработкиПредопределенных(МенеджерОбъекта, Таблица, Ложь);
	
КонецПроцедуры

Процедура Тест_МенеджерВходитВПодсистему(ИмяОбъекта, ОжидаемоеЗначение) Экспорт
	
	МенеджерОбъекта = Неопределено;
	Если ЗначениеЗаполнено(ИмяОбъекта) Тогда
		МенеджерОбъекта = пбп_Переадресация.МенеджерОбъектаПоПолномуИмени(ИмяОбъекта);
	КонецЕсли;
	
	Результат = пбп_ПредопределенныеЗначенияСлужебный.МенеджерВходитВПодсистемуПредопределенных(МенеджерОбъекта);
	
	Если ОжидаемоеЗначение Тогда
		ЮТест.ОжидаетЧто(Результат).ЭтоИстина();
	Иначе
		ЮТест.ОжидаетЧто(Результат).ЭтоЛожь();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаРезультатовОбработкиПредопределенных(МенеджерОбъекта, Таблица, МенеджерИзПодсистемы = Истина)
	
	Результат = пбп_ПредопределенныеЗначенияСлужебный
		.ДопПараметрыОбработкиПредопределенныхЭлементов(Таблица, МенеджерОбъекта);
	
	ЮТест.ОжидаетЧто(ТипЗнч(Результат)).Равно(Тип("Структура"));
	
	ЮТест.ОжидаетЧто(Результат)
		.ИмеетСвойство("Менеджер")
		.ИмеетСвойство("ПолноеИмя")
		.ИмеетСвойство("Иерархический")
		.ИмеетСвойство("КлючиХешаЭлемент")
		.ИмеетСвойство("КлючиХешаГруппа");
	
	ЮТест.ОжидаетЧто(ТипЗнч(Результат.ПолноеИмя)).Равно(Тип("Строка"));
	ЮТест.ОжидаетЧто(ТипЗнч(Результат.Иерархический)).Равно(Тип("Булево"));
	ЮТест.ОжидаетЧто(ТипЗнч(Результат.КлючиХешаЭлемент)).Равно(Тип("Структура"));
	ЮТест.ОжидаетЧто(ТипЗнч(Результат.КлючиХешаГруппа)).Равно(Тип("Структура"));
	Если МенеджерИзПодсистемы И ЗначениеЗаполнено(Таблица.Колонки) Тогда
		ЮТест.ОжидаетЧто(ЗначениеЗаполнено(Результат.КлючиХешаЭлемент)).Равно(Истина);
		ЮТест.ОжидаетЧто(ЗначениеЗаполнено(Результат.КлючиХешаГруппа)).Равно(Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
