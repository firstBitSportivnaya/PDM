// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавить запись.
// 
// Параметры:
//  ПараметрыЗаписи - Структура - набор параметров Для записи. Ключи:
//   * Бот - СправочникСсылка.Боты - Бот
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь бота
//   * Задание - СправочникСсылка.ЗаданияБотов - Задание бота
//   * НомерШага - Число - Номер шага выполнения
//   * ИдентификаторСообщения - Число - Идентификатор сообщения
//   * ДатаСообщения - Дата - Дата время сообщения в UTC-0
Процедура ДобавитьЗапись(ПараметрыЗаписи) Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗаписи);
	МенеджерЗаписи.Период = ТекущаяДатаСеанса();
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Изменить запись.
// 
// Параметры:
//  ПараметрыЗаписи - Структура - набор параметров Для записи. Ключи:
//   * Бот - СправочникСсылка.Боты - Бот
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь бота
//   * Задание - СправочникСсылка.ЗаданияБотов - Задание бота
//   * НомерШага - Число - Номер шага выполнения
//   * ИдентификаторСообщения - Число - Идентификатор сообщения
//   * ОтветПользователя - Строка - Сообщение пользователя
//   * ДатаСообщения - Дата - Дата время сообщения в UTC-0
//  БезИдентификатора - Булево - Если Истина, то отбор записи по идентификатору сообщения не ставится
Процедура ИзменитьЗапись(ПараметрыЗаписи, БезИдентификатора) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	
	Если ПараметрыЗаписи.Свойство("Период") И ЗначениеЗаполнено(ПараметрыЗаписи.Период) Тогда
		НаборЗаписей.Отбор.Период.Установить(ПараметрыЗаписи.Период);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаписи.Бот) Тогда
		НаборЗаписей.Отбор.Бот.Установить(ПараметрыЗаписи.Бот);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаписи.Пользователь) Тогда
		НаборЗаписей.Отбор.Пользователь.Установить(ПараметрыЗаписи.Пользователь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаписи.Задание) Тогда
		НаборЗаписей.Отбор.Задание.Установить(ПараметрыЗаписи.Задание);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаписи.НомерШага) Тогда
		НаборЗаписей.Отбор.НомерШага.Установить(ПараметрыЗаписи.НомерШага);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаписи.ИдентификаторСообщения) И Не БезИдентификатора Тогда
		НаборЗаписей.Отбор.ИдентификаторСообщения.Установить(ПараметрыЗаписи.ИдентификаторСообщения);
	КонецЕсли;
	
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() Тогда
		Запись = НаборЗаписей[0];
		Для Каждого КлючИЗначение Из ПараметрыЗаписи Цикл
			Запись[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
		КонецЦикла;
		
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Удалить запись.
// 
// Параметры:
//  ПараметрыЗаписи - Структура - набор параметров Для записи. Ключи:
//   * Бот - СправочникСсылка.Боты - Бот
//   * Пользователь - СправочникСсылка.Пользователи - Пользователь бота
//   * Задание - СправочникСсылка.ЗаданияБотов - Задание бота
//   * НомерШага - Число - Номер шага выполнения
//   * ИдентификаторСообщения - Число - Идентификатор сообщения
Процедура УдалитьЗаписи(ПараметрыЗаписи) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	Для Каждого КлючИЗначение Из ПараметрыЗаписи Цикл
		Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
	КонецЦикла;
	
	НаборЗаписей.Очистить();
	НаборЗаписей.Записать();
	
КонецПроцедуры

Процедура ОчиститьПоРасписанию() Экспорт
	
	КолДнейХранения = ОбщегоНазначенияУПОПовтИсп.ПолучитьКоличествоДнейХраненияВыполненияЗаданийБотов();
	
	Если Не ЗначениеЗаполнено(КолДнейХранения) Тогда
		Возврат;
	КонецЕсли;
	
	ДатаАктуальности = НачалоДня(ТекущаяДатаСеанса()) - 86400 * КолДнейХранения;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыполнениеЗаданийБотов.Период КАК Период,
	|	ВыполнениеЗаданийБотов.Бот КАК Бот,
	|	ВыполнениеЗаданийБотов.Пользователь КАК Пользователь,
	|	ВыполнениеЗаданийБотов.Задание КАК Задание,
	|	ВыполнениеЗаданийБотов.НомерШага КАК НомерШага,
	|	ВыполнениеЗаданийБотов.ИдентификаторСообщения КАК ИдентификаторСообщения
	|ИЗ
	|	РегистрСведений.ВыполнениеЗаданийБотов КАК ВыполнениеЗаданийБотов
	|ГДЕ
	|	ВыполнениеЗаданийБотов.Период < &ДатаАктуальности";
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Попытка
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ВыполнениеЗаданийБотов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаДетальныеЗаписи);
			МенеджерЗаписи.Прочитать();
			
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		
		ИмяСобытия = НСтр("ru = 'Очистка выполнения заданий ботов'");
		ЗаголовокОшибки = СтрШаблон("Не удалось удалить запись выполнения заданий ботов");
		
		ТекстОшибки = ОбщегоНазначенияУПОСервер.ПолучениеПолногоТекстаОшибкиПриИсключении(ЗаголовокОшибки, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), ПолучитьСообщенияПользователю(Истина));
		ЖурналРегистрации.ДобавитьСообщениеДляЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#КонецЕсли