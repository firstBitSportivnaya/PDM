// @strict-types
#Область ОписаниеПеременных

// СтандартныеПодсистемы.ОценкаПроизводительности
&НаКлиенте
Перем ИдентификаторЗамераПроведение;
// Конец СтандартныеПодсистемы.ОценкаПроизводительности

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	ПодготовитьФормуНаСервере();
	УстановитьВидимостьЭлементов();
	УстановитьДоступностьЭлементов();
	УстановитьЗаголовокФормы();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ИдентификаторЗамераПроведение = ОценкаПроизводительностиКлиент.ЗамерВремени();
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.УстановитьКлючевуюОперациюЗамера(ИдентификаторЗамераПроведение,
			"КадровыеИзмененияПроведение");
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ОценкаПроизводительности
	
	УстановитьЗаголовокФормы();

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервере
Процедура ВидСобытияПриИзмененииНаСервере()
	
	Если Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перевод
		Или Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
		Объект.Отдел		= Справочники.Отделы.ПустаяСсылка();
		Объект.Должность	= Справочники.Должности.ПустаяСсылка();
	КонецЕсли;
	
	ЗаполнитьНаставникаВКадровомПереводе(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидСобытияПриИзменении(Элемент)

	ВидСобытияПриИзмененииНаСервере();
	ЗаполнитьСвязьПараметровВыбораДляОтдела();

	УстановитьВидимостьЭлементов();
	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ДатаСобытияПриИзменении(Элемент)
	
	ЗаполнитьНаставникаВКадровомПереводе(Ложь);

КонецПроцедуры

&НаСервере
Процедура СотрудникПриИзмененииНаСервере()
	
	Если Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перевод
		Или Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение Тогда
		ТаблицаКадровойИнформации = РегистрыСведений.КадроваяИнформация.СрезПоследних(
			ТекущаяДатаСеанса(), Новый Структура("Сотрудник", Объект.Сотрудник));
		
		Если ТаблицаКадровойИнформации <> Неопределено И ТаблицаКадровойИнформации.Количество() Тогда
			Объект.Офис = ТаблицаКадровойИнформации[0].Офис;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьНаставникаВКадровомПереводе(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбучаемыйПриИзменении(Элемент)
	
	Элементы.Наставник.Видимость = Объект.Обучаемый;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьОтделПриИзменении(Элемент)

	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДолжностьПриИзменении(Элемент)

	УстановитьДоступностьЭлементов();

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

//@skip-check module-unused-method
#Область СтандартныеПодсистемы

// Подключаемый выполнить команду.
// 
// Параметры:
//  Команда - КомандаФормы
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);

КонецПроцедуры

// Подключаемый выполнить команду на сервере.
// 
// Параметры:
//  Контекст  - Структура
//  Результат - Структура
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);

КонецПроцедуры

#КонецОбласти // СтандартныеПодсистемы

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	МассивВидовСобытий = Новый Массив;
	МассивВидовСобытий.Добавить(Перечисления.ВидыКадровыхСобытий.Прием);
	МассивВидовСобытий.Добавить(Перечисления.ВидыКадровыхСобытий.Перевод);
	МассивВидовСобытий.Добавить(Перечисления.ВидыКадровыхСобытий.Увольнение);

	Элементы.ВидСобытия.СписокВыбора.ЗагрузитьЗначения(МассивВидовСобытий);

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Ответственный = Пользователи.ТекущийПользователь();
		
		Если Параметры.ЗначенияЗаполнения.Свойство("ВидОперации") Тогда
			Объект.ВидОперации = Параметры.ЗначенияЗаполнения.ВидОперации;
		КонецЕсли;
	Иначе
		Если Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перевод Тогда
			Если ЗначениеЗаполнено(Объект.Отдел) Тогда
				ИзменитьОтдел = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Объект.Должность) Тогда
				ИзменитьДолжность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСвязьПараметровВыбораДляОтдела();

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()

	ЭтоПеревод = Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перевод;
	Элементы.ИзменитьОтдел.Видимость		= ЭтоПеревод;
	Элементы.ИзменитьДолжность.Видимость	= ЭтоПеревод;

	ЭтоУвольнение = Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
	Элементы.Отдел.Видимость		= Не ЭтоУвольнение;
	Элементы.Должность.Видимость	= Не ЭтоУвольнение;
	
	// Наставничество
	Элементы.ГруппаНаставничество.Видимость	= Не ЭтоУвольнение;
	Элементы.Наставник.Видимость			= Объект.Обучаемый;

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементов()

	Если Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перевод Тогда
		Элементы.Отдел.Доступность		= ИзменитьОтдел;
		Элементы.Должность.Доступность	= ИзменитьДолжность;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()

	ТекстЗаголовка = НСтр("ru = 'Кадровое изменение'; en = 'Personnel change'");
	ТекстЗаголовка = ТекстЗаголовка + НСтр(
		"ru = ' (прием, перевод, увольнение)'; en = ' (recruitment, transfer, dismissal)'");

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ТекстЗаголовка = ТекстЗаголовка + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = ' %1 от %2'; en = ' %1 from %2'"), Объект.Номер, Формат(Объект.Дата, "ДФ=dd.MM.yyyy;"));
	Иначе
		ТекстЗаголовка = ТекстЗаголовка + НСтр("ru = ' (создание)'; en = ' (create)'");
	КонецЕсли;

	ЭтотОбъект.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвязьПараметровВыбораДляОтдела()
	
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Офис", РежимИзмененияСвязанногоЗначения.Очищать);
	НовыйМассив = Новый Массив;
	НовыйМассив.Добавить(НоваяСвязь);
	НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
	Элементы.Отдел.СвязиПараметровВыбора = НовыеСвязи;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаставникаВКадровомПереводе(ПроверятьНаНовыйДокумент)
	
	Если Не Объект.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перевод Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверятьНаНовыйДокумент Тогда
		Если Не Объект.Ссылка.Пустая() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДатаСобытия) И ЗначениеЗаполнено(Объект.Сотрудник) Тогда
		Объект.Наставник = КадровыйУчетСервер.ПолучитьАктивныхНаставниковСотрудника(Объект.ДатаСобытия, Объект.Сотрудник);
		
		Если ЗначениеЗаполнено(Объект.Наставник) Тогда
			Объект.Обучаемый				= Истина;
			Элементы.Наставник.Видимость	= Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
