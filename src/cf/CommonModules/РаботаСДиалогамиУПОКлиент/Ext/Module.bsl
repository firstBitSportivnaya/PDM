////////////////////////////////////////////////////////////////////////////////
// Работа с диалогами (клиент): для клиентских методов работы с диалогами.
#Область ПрограммныйИнтерфейс

// Осуществляет выбор составного типа из списка в управляемой форме.
//
// Параметры:
//  ТекущаяФорма  - Форма
//  ЭлементВладелец - ЭлементУправления
//  Контейнер     - Объект,СтрокаТабличнойЧасти
//  ИмяРеквизита  - Строка
//  СписокВыбора  - Список с типами, которые выбираем
//  СтандартнаяОбработка - Булево
//
Процедура НачалоВыбораСоставного(ТекущаяФорма, ЭлементВладелец, Контейнер, ИмяРеквизита, СписокВыбора,
	СтандартнаяОбработка) Экспорт

	Если Контейнер = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ЭлементВладелец.ВыбиратьТип = Ложь;

	ТекущееЗначение = Контейнер[ИмяРеквизита];
	Если ТекущееЗначение = Неопределено Тогда

		СтандартнаяОбработка = Ложь;
		ЭлементВладелец.ВыбиратьТип = Истина;

		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Контейнер", Контейнер);
		ДополнительныеПараметры.Вставить("ИмяРеквизита", ИмяРеквизита);
		ДополнительныеПараметры.Вставить("ЭлементВладелец", ЭлементВладелец);
		ДополнительныеПараметры.Вставить("СтандартнаяОбработка", СтандартнаяОбработка);
		ДополнительныеПараметры.Вставить("ФормаВладелец", ТекущаяФорма);
		ТекущаяФорма.ПоказатьВыборИзСписка(
			Новый ОписаниеОповещения("НачалоВыбораСоставного_Завершение", ЭтотОбъект, ДополнительныеПараметры),
			СписокВыбора);
	КонецЕсли;

КонецПроцедуры

Процедура ОткрытиеСоставного(ТекущаяФорма, ЭлементВладелец, СтандартнаяОбработка, Контейнер) Экспорт

	Если Контейнер = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(Контейнер.ЗначениеАдрес) Тогда
		СтандартнаяОбработка = Ложь;
		
		ТипСтрокой = РаботаСДиалогамиУПОВызовСервера.ПолучитьТипЗначенияИзХранилищаСтрокой(Контейнер.ЗначениеАдрес);
		
		Если ТипСтрокой = "Структура" Или ТипСтрокой = "Массив" Тогда
			ПараметрыОткрытия = Новый Структура;
			
			Если ТипСтрокой = "Структура" Тогда
				ПараметрыОткрытия.Вставить("ЗначенияСтруктуры", Контейнер.ЗначениеАдрес);
			Иначе
				ПараметрыОткрытия.Вставить("ЗначенияМассива", Контейнер.ЗначениеАдрес);
			КонецЕсли;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Контейнер"		, Контейнер);
			ДополнительныеПараметры.Вставить("ЭлементВладелец"	, ЭлементВладелец);
			ДополнительныеПараметры.Вставить("Тип"				, Тип(ТипСтрокой));
			
			ОписаниеОЗавершении = Новый ОписаниеОповещения("НачалоВыбораЗначения_Завершение", РаботаСДиалогамиУПОКлиент, ДополнительныеПараметры);
			ОткрытьФорму("Справочник.ПользовательскиеФункции.Форма.ФормаЗаполненияСтруктурыМассива", ПараметрыОткрытия,
				ТекущаяФорма, Новый УникальныйИдентификатор, , , ОписаниеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		Иначе
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("ЗначенияТаблицы", Контейнер.ЗначениеАдрес);
				
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Контейнер"		, Контейнер);
			ДополнительныеПараметры.Вставить("ЭлементВладелец"	, ЭлементВладелец);
			ДополнительныеПараметры.Вставить("Тип"				, Тип("Соответствие"));
			
			ОписаниеОЗавершении = Новый ОписаниеОповещения("НачалоВыбораЗначения_Завершение", РаботаСДиалогамиУПОКлиент, ДополнительныеПараметры);
			ОткрытьФорму("Справочник.ПользовательскиеФункции.Форма.ФормаЗаполненияТаблицыЗначений", ПараметрыОткрытия,
				ТекущаяФорма, Новый УникальныйИдентификатор, , , ОписаниеОЗавершении, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

Процедура НачалоВыбораСоставного_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("Тип", Результат.Значение);
	ОписаниеОЗавершении = Новый ОписаниеОповещения("НачалоВыбораЗначения_Завершение", ЭтотОбъект, ДополнительныеПараметры);
	Подсказка = "Введите значение параметра";
	
	Если Результат.Значение = Тип("Строка") Тогда
		ПоказатьВводСтроки(ОписаниеОЗавершении, , Подсказка);
		
	ИначеЕсли Результат.Значение = Тип("Дата") Тогда
		ПоказатьВводДаты(ОписаниеОЗавершении, , Подсказка);
		
	ИначеЕсли Результат.Значение = Тип("Булево") Тогда
		ПоказатьВводЗначения(ОписаниеОЗавершении, Истина, Подсказка);
		
	ИначеЕсли Результат.Значение = Тип("Число") Тогда
		ПоказатьВводЧисла(ОписаниеОЗавершении, , Подсказка);
		
	ИначеЕсли Результат.Значение = Тип("Структура") Тогда
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.Контейнер.ЗначениеАдрес) Тогда
			ЗначенияСтруктуры = ДополнительныеПараметры.Контейнер.ЗначениеАдрес;
		Иначе
			ЗначенияСтруктуры = ПоместитьВоВременноеХранилище(Новый Структура, Новый УникальныйИдентификатор);
		КонецЕсли;
		
		ПараметрыОткрытия = Новый Структура("ЗначенияСтруктуры", ЗначенияСтруктуры);
		
		ОткрытьФорму("Справочник.ПользовательскиеФункции.Форма.ФормаЗаполненияСтруктурыМассива", ПараметрыОткрытия,
			ДополнительныеПараметры.ФормаВладелец, Новый УникальныйИдентификатор, , , ОписаниеОЗавершении);
		
	ИначеЕсли Результат.Значение = Тип("Массив") Тогда
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.Контейнер.ЗначениеАдрес) Тогда
			ЗначенияМассива = ДополнительныеПараметры.Контейнер.ЗначениеАдрес;
		Иначе
			ЗначенияМассива = ПоместитьВоВременноеХранилище(Новый Массив, Новый УникальныйИдентификатор);
		КонецЕсли;
		
		ПараметрыОткрытия = Новый Структура("ЗначенияМассива", ЗначенияМассива);
		
		ОткрытьФорму("Справочник.ПользовательскиеФункции.Форма.ФормаЗаполненияСтруктурыМассива", ПараметрыОткрытия,
			ДополнительныеПараметры.ФормаВладелец, Новый УникальныйИдентификатор, , , ОписаниеОЗавершении);
		
	ИначеЕсли Результат.Значение = Тип("Соответствие") Тогда
		
		ПараметрыОткрытия = Новый Структура;
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.Контейнер.ЗначениеАдрес) Тогда
			ПараметрыОткрытия.Вставить("ЗначенияТаблицы", ДополнительныеПараметры.Контейнер.ЗначениеАдрес);
		КонецЕсли;
		
		ОткрытьФорму("Справочник.ПользовательскиеФункции.Форма.ФормаЗаполненияТаблицыЗначений", ПараметрыОткрытия,
			ДополнительныеПараметры.ФормаВладелец, Новый УникальныйИдентификатор, , , ОписаниеОЗавершении);
		
	Иначе
		Если ОбщегоНазначенияУПОВызовСервера.ЭтоТипОбъектаСсылочногоТипа(Результат.Значение) Тогда
			ИмяФормыВыбора = РаботаСДиалогамиУПОВызовСервера.ПолучитьФормуВыбораСсылочногоТипа(Результат.Значение);
			
			ОткрытьФорму(ИмяФормыВыбора, , ДополнительныеПараметры.ФормаВладелец,
				Новый УникальныйИдентификатор, , , ОписаниеОЗавершении);
		ИначеЕсли ОбщегоНазначенияУПОВызовСервера.ЭтоТипПеречисление(Результат.Значение) Тогда
			СписокВыбора = РаботаСДиалогамиУПОВызовСервера.ПолучитьЗначенияПеречисленияПоТипу(Результат.Значение);
			
			ДополнительныеПараметры.ФормаВладелец.ПоказатьВыборИзСписка(
				Новый ОписаниеОповещения("НачалоВыбораЗначения_Завершение", ЭтотОбъект, ДополнительныеПараметры),
				СписокВыбора);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачалоВыбораЗначения_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Тип = Тип("Структура") Тогда
		ДополнительныеПараметры.Контейнер.Значение = "Структура";
		ДополнительныеПараметры.Контейнер.ЗначениеАдрес = Результат;
		ДополнительныеПараметры.ЭлементВладелец.КнопкаОткрытия = Истина;
	ИначеЕсли ДополнительныеПараметры.Тип = Тип("Массив") Тогда
		ДополнительныеПараметры.Контейнер.Значение = "Массив";
		ДополнительныеПараметры.Контейнер.ЗначениеАдрес = Результат;
		ДополнительныеПараметры.ЭлементВладелец.КнопкаОткрытия = Истина;
	ИначеЕсли ДополнительныеПараметры.Тип = Тип("Соответствие") Тогда
		ДополнительныеПараметры.Контейнер.Значение = "Таблица значений";
		ДополнительныеПараметры.Контейнер.ЗначениеАдрес = Результат;
		ДополнительныеПараметры.ЭлементВладелец.КнопкаОткрытия = Истина;
	Иначе
		ДополнительныеПараметры.Контейнер.Значение = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции