// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

    //Ограничение.Текст = "РазрешитьЧтение";

КонецПроцедуры // ПриЗаполненииОграниченияДоступа

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти // ДляВызоваИзДругихПодсистем

// Подготовить параметры проведения.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка.Обучения - ссылка на документ
//  Отказ          - Булево
// 
// Возвращаемое значение:
//  Структура - Подготовить параметры проведения
Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	Реквизиты        = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	ПараметрыПроведения.Вставить("Реквизиты", Реквизиты);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
	
	Если Не ПустаяСтрока(Запрос.Текст) Тогда
		Результат = Запрос.ВыполнитьПакет();
	КонецЕсли;
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции // ПодготовитьПараметрыПроведения

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Текст запроса реквизиты документа.
// 
// Параметры:
//  НомераТаблиц - Структура - Номера таблиц
// 
// Возвращаемое значение:
//  Строка - Текст запроса реквизиты документа
Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты"	, НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты"					, НомераТаблиц.Количество());
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Номер,
		|	ДанныеДокумента.Дата,
		|	ДанныеДокумента.ПометкаУдаления,
		|	ДанныеДокумента.Проведен,
		|	ДанныеДокумента.Ответственный,
		|	ДанныеДокумента.Комментарий
		|ПОМЕСТИТЬ Реквизиты
		|ИЗ
		|	Документ.РегистрацияРабочегоВремени КАК ДанныеДокумента
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Реквизиты.*
		|ИЗ
		|	Реквизиты КАК Реквизиты";
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

// Текст запроса временные таблицы документа.
// 
// Параметры:
//  НомераТаблиц - Структура - Номера таблиц
//  Реквизиты - ТаблицаЗначений - Реквизиты
// 
// Возвращаемое значение:
//  Строка - Текст запроса временные таблицы документа
Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)
	
	ТекстЗапроса = "";
	
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	РегистрацияРабочегоВремениРабочееВремя.Ссылка КАК Ссылка,
		|	РегистрацияРабочегоВремениРабочееВремя.НомерСтроки КАК НомерСтроки,
		|	РегистрацияРабочегоВремениРабочееВремя.ДатаРегистрации КАК ДатаРегистрации,
		|	РегистрацияРабочегоВремениРабочееВремя.Проект КАК Проект,
		|	РегистрацияРабочегоВремениРабочееВремя.ЗадачаПроекта КАК ЗадачаПроекта,
		|	РегистрацияРабочегоВремениРабочееВремя.Сотрудник КАК Сотрудник,
		|	РегистрацияРабочегоВремениРабочееВремя.ДатаСписания КАК ДатаСписания,
		|	РегистрацияРабочегоВремениРабочееВремя.Часы КАК Часы,
		|	РегистрацияРабочегоВремениРабочееВремя.ОписаниеРаботы КАК ОписаниеРаботы,
		|	РегистрацияРабочегоВремениРабочееВремя.ИдентификаторЗаписи КАК ИдентификаторЗаписи
		|ИЗ
		|	Документ.РегистрацияРабочегоВремени.РабочееВремя КАК РегистрацияРабочегоВремениРабочееВремя
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	РегистрацияРабочегоВремениРабочееВремя.Ссылка = &Ссылка"
	+ ОбщегоНазначения.РазделительПакетаЗапросов();
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРабочееВремя", НомераТаблиц.Количество());
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

#КонецЕсли